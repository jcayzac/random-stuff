REM ============================================================================
REM =             RUN THE AUTOMATED TESTS FOR THE PLATFORMSCOPE                =
REM ============================================================================
REM TODO:
REM - Include the code coverage analysis, with MSVC or Emma (maybe based on the
REM   'platformscope' env value, or a new env variable).
REM - Get rid of any piece of remaining Perl junk.

REM ----------------------------------------------------------------------------
REM Set some environment variables that will likely go elsewhere in the future.
REM ----------------------------------------------------------------------------

REM - Where the tests are. This is the directory with all the $platformscope subdirectories -
set tests_root=\tools\test
REM - Where the test framework lies (the directory which contains both execution/ and stylesheets/) -
set testframework_root=\testframework
REM - Some temp dir -
set report_tmp=c:\temp\%platformscope%

REM !!! Should be dynamically generated by the autobuild scripts. !!!

REM - Where the reference files are -
set reference_file=\\references\%platformscope%.zip
REM - Where the raw results are -
set raw_results=%epocdrive%%epocroot%epoc32\%testtarget%\c\testlogs
REM - Where to write the test report
set test_report=%repositorypath%\test

REM ----------------------------------------------------------------------------
REM If no test is defined, do nothing and bail out.
REM ----------------------------------------------------------------------------
if not exist %tests_root%\%platformscope%\suites goto :EOF
cd %tests_root%\%platformscope%

REM ----------------------------------------------------------------------------
REM Create temp dir
REM ----------------------------------------------------------------------------
rmdir /s /q %report_tmp%
mkdir %report_tmp%
set test_report_save=%test_report%
set test_report=%report_tmp%


REM ----------------------------------------------------------------------------
REM Prepare the CPP code coverage analysis tool
REM ----------------------------------------------------------------------------

if defined USE_CPP_CODE_COVERAGE (
	if %USE_CPP_CODE_COVERAGE%==TRUE (
		perl -Is:\testframework\execution\modules\perl -Is:\testframework\execution\scripts\perl s:\testframework\CPPCoverage\pre_processing.pl || echo Could not execute perl script
	)
)

REM ----------------------------------------------------------------------------
REM Execute the pre-tests scriptset. These scripts are executed in alphabetical
REM order before the test suites get executed. It's a useful place for e.g. 
REM setting up files needed by the tests.
REM ----------------------------------------------------------------------------
echo Preparation phase
FOR /F "usebackq delims==" %%I IN (`dir /O:N /B %tests_root%\%platformscope%\execution\pre`) DO (
	echo Running preparation script %%I
	call %tests_root%\%platformscope%\execution\pre\%%I
	echo Done with preparation script %%I
)

REM ----------------------------------------------------------------------------
REM Execute the tests suites found in \tools\test\%platformscope%\suites, in
REM alphabetical order (multiple test suites are allowed).
REM ----------------------------------------------------------------------------
FOR /F "usebackq delims==" %%I IN (`dir /O:N /B %tests_root%\%platformscope%\suites`) DO (
	echo Running test suite %%I
	call %testframework_root%\execution\scripts\python\TestManager.py %tests_root%\%platformscope%\suites\%%I
	echo Done with test suite %%I
)

REM ----------------------------------------------------------------------------
REM Execute the post-tests scriptset. These scripts are executed in alphabetical
REM order after the test suite are done, so it's the natural place for post
REM processing test results before the report generator gets called.
REM ----------------------------------------------------------------------------
echo Post-processing phase
FOR /F "usebackq delims==" %%I IN (`dir /O:N /B %tests_root%\%platformscope%\execution\post`) DO (
	echo Running post-processing script %%I
	call %tests_root%\%platformscope%\execution\post\%%I
	echo Done with post-processing script %%I
)

REM ----------------------------------------------------------------------------
REM Generate the XML/HTML test report.
REM ----------------------------------------------------------------------------

call \testframework\execution\scripts\python\GenerateReport.py

set test_report=%test_report_save%
xcopy /y /s /i %report_tmp% %test_report%

REM ----------------------------------------------------------------------------
REM Generate CPP code coverage analysis report
REM ----------------------------------------------------------------------------

if defined USE_CPP_CODE_COVERAGE (
	if %USE_CPP_CODE_COVERAGE%==TRUE (
		perl -Is:\testframework\execution\modules\perl -Is:\testframework\execution\scripts\perl s:\testframework\CPPCoverage\post_processing.pl || echo Could not execute perl script
	)
)

:EOF
